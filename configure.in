dnl Process this file with autoconf to produce a configure script.
AC_INIT(parse.c)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libdv, 0.1)
AC_CANONICAL_HOST
AM_MAINTAINER_MODE
AC_DISABLE_SHARED

dnl Checks for programs.
CFLAGS="-O2 -g -Wall"
AC_SUBST(CFLAGS)
AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

use_asm=:
arch_x86=false

AC_ARG_ENABLE(asm,
[  --disable-asm           disable use of architecture specific assembly code],
[	if test "$enableval" = "no"; then
		use_asm=false
	fi
])

use_sdl=:
AC_ARG_ENABLE(sdl,
[  --disable-sdl           disable use of SDL for display],
[     if test "$enableval" = "no"; then
               use_sdl=false
       fi
])

use_xv=:
AC_ARG_ENABLE(xv,
[  --disable-xv           disable use of XVideo extension for display],
[     if test "$enableval" = "no"; then
               use_xv=false
       fi
])

if $use_asm; then
	case "$host_cpu" in
		i?86)
			arch_x86=:
			AC_DEFINE(ARCH_X86)
			;;
		*)
			;;
	esac
fi

AM_CONDITIONAL(HOST_X86, $arch_x86)

dnl Checks for libraries.

ifdef([AM_PATH_GLIB],[
	AM_PATH_GLIB(,
	[
		AC_DEFINE(HAVE_GLIB)
		LIBS="$LIBS $GLIB_LIBS"
		CFLAGS="$CFLAGS $GLIB_CFLAGS"
	],
	[
		AC_MSG_ERROR([No glib. Bad.  Get it at:  http://www.gtk.org/.])
	])
],[
	AC_MSG_ERROR([No glib. Bad. Get it at:  http://www.gtk.org/])
])

ifdef([AM_PATH_GTK],[
	AM_PATH_GTK(,
	[
		AC_DEFINE(HAVE_GTK)
		LIBS="$LIBS $GTK_LIBS"
		CFLAGS="$CFLAGS $GTK_CFLAGS"
	])
])

ifdef([AM_PATH_SDL],[
	if $use_sdl; then
      		AM_PATH_SDL(1.1.6,
		 [
	              AC_DEFINE(HAVE_SDL)
	              LIBS="$LIBS $SDL_LIBS"
	              CFLAGS="$CFLAGS $SDL_CFLAGS"
		 ])
	fi
])

if $use_xv; then
      AC_CHECK_LIB(Xv,XvQueryAdaptors,)
fi
AC_CHECK_LIB(m, sqrt)
AC_CHECK_LIB(popt, poptGetContext,,
      [
	AC_MSG_WARN(playdv needs the popt package for command line parsing.
 		    The source archive for popt is available at:  
			ftp://ftp.redhat.com/pub/redhat/code/popt)
      ])

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl Checks for library functions.
AC_FUNC_MMAP
AC_CHECK_FUNCS(gettimeofday)

AC_SUBST(ac_aux_dir)

AC_OUTPUT(Makefile)
